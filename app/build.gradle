apply plugin: 'com.android.application'
//butterKnife使用
//apply plugin: 'com.neenbedankt.android-apt'
//使用greenDao
apply plugin: 'org.greenrobot.greendao'
//AspectJX
apply plugin: 'android-aspectjx'
//apt
//apply plugin: 'com.neenbedankt.android-apt'
//fir plugin 必填
//apply plugin: 'im.fir.plugin.gradle'

// 依赖插件脚本
apply from: 'tinker-support.gradle'
// 多渠道使用walle
apply from: 'multiple-channel.gradle'
//多渠道打包命令：gradle assembleReleaseChannels


android {
//    compileSdkVersion 'android-P'
//    compileSdkVersion 28
    compileSdkVersion rootProject.ext.android.VERSION_COMPILE_SDK
    buildToolsVersion rootProject.ext.android.VERSION_BUILD_TOOLS

    defaultConfig {
        minSdkVersion rootProject.ext.android.VERSION_MIN_SDK
        targetSdkVersion rootProject.ext.android.VERSION_TARGET_SDK
//        targetSdkVersion 'P'
//        targetSdkVersion 28
        applicationId "com.ljy.ljyutils"
        versionCode 115
        versionName "1.1.5"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        // 开启multidex
        multiDexEnabled true//解决method ID not in [0, 0xffff]: 65536 这个问题
        //下面一行用来解决以下问题
//         Error:Execution failed for task ':app:javaPreCompileDebug'.
//        > Annotation processors must be explicitly declared now.  The following dependencies on the compile classpath are found to contain annotation processor.  Please add them to the annotationProcessor configuration.
//        - processor.jar (project :processor)
//        Alternatively, set android.defaultConfig.javaCompileOptions.annotationProcessorOptions.includeCompileClasspath = true to continue with previous behavior.  Note that this option is deprecated and will be removed in the future.
//                See https://developer.android.com/r/tools/annotation-processor-error-message.html for more details.
        javaCompileOptions { annotationProcessorOptions { includeCompileClasspath = true } }
        resValue("string", "app_name", "LjyDemo")
        //resValue还可以这样
        resValue("string", "build_time", buildTime())
        //java类中调用上面创建的resValue
        //L.d(tag,getString(R.string.build_time));
    }

//    fir {
//
//        //必填 上传 fir.im apk token 字段，否则无法上传 APP 到 fir.im
////        apiToken '0eb90076384aaab473d52a4f003774b3'
//        apiToken '0a4ed25d1426bd7bc6d147a3504cc3e7'
//
//        //可选 上传fir.im 中显示的changelog
//        changeLog '第一次上传fir'
//    }

    //fir可选 如需实现自动上传符号表到 BugHD 功能，为必填项
//    bughd {
//        projectId '�替换为你的 BugHD PROJECT_ID'
//        apiToken '�替换为你的 BugHD API_TOKEN'
//    }

    //指定java的编译版本
    compileOptions {
        sourceCompatibility rootProject.ext.android.SOURCE_COMPATIBILITY
        targetCompatibility rootProject.ext.android.TARGET_COMPATIBILITY
    }

    //Gradle编译禁用Lint报错，link check到error时会继续编译
    lintOptions {
        abortOnError false
    }

    //用来关闭Android Studio的PNG合法性检查的，直接不让它检查
    //（添加这一行的原因是girl.png并不是一个合法的png图片，其本质是个jpeg图片）
    aaptOptions {
        cruncherEnabled = false//AAPT检查，关上会快，但是release版本建议打开
        useNewCruncher = false
    }

//    //解决与module中v4,v7包冲突问题
//    configurations {
//        all*.exclude group: 'com.android.support', module: 'support-v4'
//        all*.exclude group: 'com.android.support', module: 'support-annotations'
//    }

    signingConfigs {
        config {
            keyAlias 'key'
            keyPassword '222222'
            storeFile file('ljyKeyStore.jks')
            storePassword '222222'
            v2SigningEnabled true
        }
    }

    buildTypes {
        release {
            buildConfigField("boolean", "isDebug", "false")
            debuggable false
            minifyEnabled true//开启代码混淆优化
            shrinkResources true // 是否去除无效的资源文件，依赖于minifyEnabled
            //对齐优化设置为true，否则找不到该task (默认不写也是true)
            zipAlignEnabled true
            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //下面一行，可以增加变量到R文件中，但R中不能有相同key的变量，此处用来为不同的buildType设置不同的appName
            resValue("string", "app_name", "LjyDemoR")
        }
        debug {
            buildConfigField("boolean", "isDebug", "true")
            minifyEnabled true
            debuggable true
            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue("string", "app_name", "LjyDemoD")
        }

        //自定义构建类型还可以继承已有的构建类型
        ljy.initWith(buildTypes.debug)
        //自定义构建类型
        ljy {
            applicationIdSuffix ".jinyang"//可以给应用的包名添加后缀，以区分不同的构建类型
            //MyApplication中打印的进程名果然添加来后缀com.ljy.ljyutils.jinyang
        }
    }

    dexOptions {
        jumboMode = true//Cannot merge new index 65562 into a non-jumbo instruction! 这个问题的解决
//        incremental true//开启增量编译
        javaMaxHeapSize "4g"//增加编译的内存资源到4g
        preDexLibraries = false //设置对第三方lib不做preDex处理
//        additionalParameters = ['--multi-dex', '--main-dex-list=' + project.rootDir.absolutePath +
//                '/app/maindexlist.txt', '--minimal-main-dex','--set-max-idx-number=65000']
        additionalParameters = ['--multi-dex', '--set-max-idx-number=65000']
        //--multi-dex：表示当方法数越界时则生成多个dex文件
        //--main-dex-list：指定来要在主dex中打包的类的列表，注意 multidex 的jar包中的9个类也必须
        //打包到主dex，否则会报错，无法找到multidex相关类
        //--minimal-main-dex：表明只有main-dex-list所指定导入类才能打包到主dex中
        //multidex所带来的问题：
        //1。app启动速度降低，甚至出现ANR异常，应避免较大的dex
        //2。由于Dalvik linearAlloc的bug，可能导致使用multidex的app无法在Android4.0以前的手机运行，
        // 可能出现产生大量内存消耗情况，需要做大量兼容性测试
        //因此最好使用插件化方案来代替multidex

    }

    //导入dataBinding支持
    dataBinding {
        enabled true
    }

    //通过sourceSets的配置可以自定义as项目的结构,只需把调整后的结构对应的写入即可
    //src/main/xxx直接写为src即可，另外本项目是Android studio默认的结构，可以不用配置这些的，
    //这里只是为来功能演示，以作记录
    sourceSets {
        main {
            java.srcDirs = ['src/main/java']
            res.srcDirs = ['src/main/res/']
            assets.srcDirs = ['src/main/assets']
            jniLibs.srcDirs = ['src/main/jniLibs']
            aidl.srcDirs = ['src/main/aidl']
            renderscript.srcDirs = ['src']
            manifest.srcFile 'src/main/AndroidManifest.xml'
        }
    }
    repositories {
        flatDir {
            dirs 'libs'
        }
    }

    aspectjx {
        excludeJarFilter '.jar'
    }

    //android原生多渠道打包配置, 实际开发中也可以使用美团walle打包
    //但是原生的配置不仅可以打出渠道信息，还可以通过占位符为不同渠道设置不同的appName，appIcon等
    //命令行执行gradle assemble，就会打出所有渠道的所有构建类型的apk
    //也可以只打指定的构建类型的apk-->gradle assembleRelease/assembleDebug
    //方式1：
//    productFlavors{
//        ljyUtilsForBaidu{
//            manifestPlaceholders=[CHANNEL_VALUE:"baidu",APP_NAME:"@string/app_name"]
//        }
//        ljyUtilsFor360{
//            manifestPlaceholders=[CHANNEL_VALUE:"360",APP_NAME:"@string/app_name"]
//        }
//        ljyUtilsForTencent{
//            manifestPlaceholders=[CHANNEL_VALUE:"tencent",APP_NAME:"@string/app_name"]
//        }
//    }
    //方式2：
    productFlavors {
        baidu {}
        tencent {}
    }
    productFlavors.all { flavor ->
        flavor.manifestPlaceholders = [CHANNEL_VALUE: name, APP_NAME: "@string/app_name"]
    }

    //打包的apk命名规则默认为app-渠道名-buildType.apk,保存路径为app/build/outputs/apk
    //下面对release版apk的命名和存放位置进行自定义
    applicationVariants.all { variant ->
        //gradle升级到4.x后，outputFile变成类只读属性,可以用 variant.outputs.all 代替
        variant.outputs.each { output ->
            if (output.outputFile != null &&
                    output.outputFile.name.endsWith('.apk') &&
                    'release'.equals(variant.buildType.name)) {
                //修改保存的路径和文件名
                def apkFile = new File(output.outputFile.getParent() + "/release",
                        "ljyUtils_${variant.flavorName}_v${variant.versionName}.apk")
                output.outputFile = apkFile
            }
        }
//        variant.outputs.all {
//            outputFileName = "ljyUtils_${variant.flavorName}_v${variant.versionName}.apk"
//        }
        tasks.all {
            if (it.name.startsWith("assemble")) {
                //复制到指定路径,并重命名
                it.doLast {
                    copy {
                        from variant.outputs.outputFile + "/release"
                        into "./build/apkRelease"
                        rename { String fileName ->
                            fileName.replace("ljyUtils", "LJYUtil")
                        }
                    }
                }
            }
        }
    }
}

def buildTime() {
    return new Date().format("yyyy-MM-dd HH:mm:ss")
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    compile 'com.android.support.constraint:constraint-layout:1.1.0'
    compile rootProject.ext.dependencies.support_v7
    //butterknife
    compile 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    //dagger2
    compile 'com.google.dagger:dagger:2.13'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.12'
    //greenDao
    compile 'org.greenrobot:greendao:3.2.0'
    compile 'org.greenrobot:greendao-generator:3.2.0'
    //multidex，解决method ID not in [0, 0xffff]: 65536 这个问题
    compile 'com.android.support:multidex:1.0.1'
    //lottie
    compile 'com.airbnb.android:lottie:2.1.0'

    //+@aar可以关闭依赖传递，即值依赖该项目，而不依赖该项目所依赖的项目(只aar，jar不受影响)
//    compile 'com.airbnb.android:lottie:2.1.0@aar'

    //强制刷新远程库，避免远程库更新后，并蒂为刷新问题
//    compile ('com.airbnb.android:lottie:2.1.0'){
//        transitive=true
//    }
    //排除一个库中引用的某些库
//    compile ('com.airbnb.android:lottie:2.1.0'){
//        exclude module:'com.android.support:appcompat-v7:27.1.1'
//        exclude module:'com.android.support:appcompat-v4:27.1.1'
//    }
    //依赖本地的aar（依赖aar可以省去对module的编译，使项目更整洁，隐藏内部实现，只是不易修改）
//    compile name:"aaa",ext:'aar'

    // 远程依赖集成方式（推荐）
    compile 'com.tencent.bugly:crashreport_upgrade:1.3.4'
    //walle
    compile 'com.meituan.android.walle:library:1.1.6'
    compile 'com.android.support:design:27.1.1'
    //AspectJ
    compile 'org.aspectj:aspectjrt:1.8.13'
    //友盟推送
    compile project(':push')
    //我的工具类lib
    compile project(':lib')
    //我的注解lib
    compile project(':processor')
    //recycleView的万能适配器
    compile 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.30'
    compile project(':PluginCoreLib')
}

apply plugin: 'com.android.application'
//butterKnife使用
//apply plugin: 'com.neenbedankt.android-apt'
//使用greenDao
apply plugin: 'org.greenrobot.greendao'
//AspectJX
apply plugin: 'android-aspectjx'
//apt
//apply plugin: 'com.neenbedankt.android-apt'

android {
//    compileSdkVersion 'android-P'
//    compileSdkVersion 28
    compileSdkVersion VERSION_COMPILE_SDK
    buildToolsVersion VERSION_BUILD_TOOLS

    defaultConfig {
        minSdkVersion VERSION_MIN_SDK
        targetSdkVersion VERSION_TARGET_SDK
//        targetSdkVersion 'P'
//        targetSdkVersion 28
        applicationId "com.ljy.ljyutils"
        versionCode 115
        versionName "1.1.5"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        // 开启multidex
        multiDexEnabled true//解决method ID not in [0, 0xffff]: 65536 这个问题
    }

    compileOptions {
        sourceCompatibility SOURCE_COMPATIBILITY
    }

    //Gradle编译禁用Lint报错
    lintOptions {
        abortOnError false
    }

    //用来关闭Android Studio的PNG合法性检查的，直接不让它检查
    //（添加这一行的原因是girl.png并不是一个合法的png图片，其本质是个jpeg图片）
    aaptOptions {
        cruncherEnabled = false
        useNewCruncher = false
    }

    signingConfigs {
        config {
            keyAlias 'key'
            keyPassword '222222'
            storeFile file('ljyKeyStore.jks')
            storePassword '222222'
            v2SigningEnabled true
        }
    }

    buildTypes {
        release {
            buildConfigField("boolean", "isDebug", "false")
            minifyEnabled true
            debuggable false
            shrinkResources true // 是否去除无效的资源文件
            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            buildConfigField("boolean", "isDebug", "true")
            minifyEnabled false
            debuggable true
            signingConfig signingConfigs.config
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    dexOptions {
        jumboMode = true//Cannot merge new index 65562 into a non-jumbo instruction! 这个问题的解决
        //incremental true
        preDexLibraries = false //设置对第三方lib不做preDex处理
        javaMaxHeapSize "4g"//java虚拟机堆内存最大值设为4g，避免oom
//        additionalParameters = ['--multi-dex', '--main-dex-list=' + project.rootDir.absolutePath +
//                '/app/maindexlist.txt', '--minimal-main-dex','--set-max-idx-number=65000']
        additionalParameters = ['--multi-dex', '--set-max-idx-number=65000']
        //--multi-dex：表示当方法数越界时则生成多个dex文件
        //--main-dex-list：指定来要在主dex中打包的类的列表，注意 multidex 的jar包中的9个类也必须
        //打包到主dex，否则会报错，无法找到multidex相关类
        //--minimal-main-dex：表明只有main-dex-list所指定导入类才能打包到主dex中
        //multidex所带来的问题：
        //1。app启动速度降低，甚至出现ANR异常，应避免较大的dex
        //2。由于Dalvik linearAlloc的bug，可能导致使用multidex的app无法在Android4.0以前的手机运行，
        // 可能出现产生大量内存消耗情况，需要做大量兼容性测试
        //因此最好使用插件化方案来代替multidex

    }

    //导入dataBinding支持
    dataBinding {
        enabled true
    }

    sourceSets {
        main {
            jniLibs.srcDirs = ['libs']
        }
    }

    repositories {
        flatDir {
            dirs 'libs'
        }
    }

    aspectjx {
        excludeJarFilter '.jar'
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    compile 'com.android.support.constraint:constraint-layout:1.1.0'
    compile 'com.android.support:appcompat-v7:26.1.0'
    //butterknife
    compile 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'
    //dagger2
    compile 'com.google.dagger:dagger:2.13'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.12'
    //greenDao
    compile 'org.greenrobot:greendao:3.2.0'
    compile 'org.greenrobot:greendao-generator:3.2.0'
    //multidex，解决method ID not in [0, 0xffff]: 65536 这个问题
    compile 'com.android.support:multidex:1.0.3'
    //lottie
    compile 'com.airbnb.android:lottie:2.1.0'
    // 远程依赖集成方式（推荐）
    compile 'com.tencent.bugly:crashreport_upgrade:1.3.4'
    //walle
    compile 'com.meituan.android.walle:library:1.1.6'
    compile 'com.android.support:design:26.1.0'
    //AspectJ
    compile 'org.aspectj:aspectjrt:1.8.13'
    //友盟推送
    compile project(':push')
    //我的工具类lib
    compile project(':lib')
    //我的注解lib
    compile project(':processor')
    //recycleView的万能适配器
    compile 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.30'
    compile project(':pluginapk')
}

// 依赖插件脚本
apply from: 'tinker-support.gradle'
// 多渠道使用walle
apply from: 'multiple-channel.gradle'
//多渠道打包命令：gradle assembleReleaseChannels